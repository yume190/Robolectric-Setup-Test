apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "yume190.com.tester10"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

apply plugin: 'android-unit-test'

//tasks.findByName("assembleDebug").dependsOn("testDebugClasses")

//由於android studiode 一個bug，必須把module的iml文件中的android sdk引用放到最下面
task pushDownJdkDependency {
    //這裏是待測試項目的iml文件名
    def imlFile = file("library.iml")
    doLast {
        try {
            def parsedXml = (new XmlParser()).parse(imlFile)
            def jdkNode = parsedXml.component[1].orderEntry.find { it.'@type' == 'jdk' }

            parsedXml.component[1].remove(jdkNode)
            //這裏是target sdk版本，只需要改數字就行
            new Node(parsedXml.component[1], 'orderEntry', ['type': 'jdk', 'jdkName': "Android API 19 Platform", 'jdkType': 'Android SDK'])
            def writer = new StringWriter()
            new XmlNodePrinter(new PrintWriter(writer)).print(parsedXml)
            imlFile.text = writer.toString()

        } catch (FileNotFoundException e) {
            // nop, iml not found
        }
    }
}
//在build之前修改iml文件
gradle.projectsEvaluated {
    preBuild.dependsOn(pushDownJdkDependency)
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.loopj.android:android-async-http:1.4.6'
    compile 'com.google.code.gson:gson:2.3.1'

    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.4'
    testCompile 'org.mockito:mockito-core:1.10.17'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
}

task showReport(type:Exec) {
    workingDir '.'

    commandLine 'open', 'build/test-report/index.html'
}